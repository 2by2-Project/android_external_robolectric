import org.gradle.internal.jvm.Jvm
import org.robolectric.gradle.DeployedRoboJavaModulePlugin
import org.robolectric.gradle.RoboJavaModulePlugin

apply plugin: RoboJavaModulePlugin
apply plugin: DeployedRoboJavaModulePlugin

// Disable annotation processor for tests
compileTestJava {
    options.compilerArgs.add("-proc:none")
}

test {
    enabled = false
}

dependencies {
    // Project dependencies
    implementation project(":annotations")
    implementation project(":shadowapi")

    // Compile dependencies
    implementation "com.google.errorprone:error_prone_annotation:2.3.4"
    implementation "com.google.errorprone:error_prone_refaster:2.3.4"
    implementation "com.google.errorprone:error_prone_check_api:2.3.4"
    compileOnly "com.google.auto.service:auto-service-annotations:1.0-rc6"
    compileOnly(AndroidSdk.MAX_SDK.coordinates) { force = true }

    annotationProcessor "com.google.auto.service:auto-service:1.0-rc6"
    annotationProcessor "com.google.errorprone:error_prone_core:2.3.4"

    // in jdk 9, tools.jar disappears!
    def toolsJar = Jvm.current().getToolsJar()
    if (toolsJar != null) {
        compile files(toolsJar)
    }

    // Testing dependencies
    testImplementation "junit:junit:4.13.1"
    testImplementation "com.google.truth:truth:${truthVersion}"
    testImplementation("com.google.errorprone:error_prone_test_helpers:2.3.4") {
        exclude group: 'junit', module: 'junit' // because it depends on a snapshot!?
    }
    testCompileOnly(AndroidSdk.MAX_SDK.coordinates) { force = true }
}
